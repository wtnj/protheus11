#INCLUDE "TOPCONN.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหออออออัอออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณ DOUGLAS   บAutor ณDOUGLAS DOURADO บ Data ณ  13/06/12       บฑฑ
ฑฑฬออออออออออุออออออออออสออออออฯอออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ RELATำRIO DE TESTE               			              บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ ESTOQUE / CUSTOS                                           บฑฑ
ฑฑฬออออออออออุออออออออออัอออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบDATA      ณ ANALISTA ณ MOTIVO                                          บฑฑ
ฑฑฬออออออออออุออออออออออุอออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบ          ณ          ณ                                                 บฑฑ
ฑฑศออออออออออฯออออออออออฯอออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function NHEST201()

cString   := "STJ"
cDesc1    := "Este   relatorio   tem   como   objetivo  mostrar o status"
cDesc2    := "de solicita็๕es de compra."
cDesc3    := ""
tamanho   := "G"
limite    := 132
aReturn   := { "Zebrado", 1,"Administracao", 1, 2, 1, "",1 }
nomeprog  := "NHEST201"
nLastKey  := 0
titulo    := "STATUS  DE  SOLICITAวรO  DE  COMPRA  -  "// Tํtulo do relat๓rio
cabec1    := " SC         Item      Produto             Descri็ใo                               Quant     C.Custo     Grupo     Emissใo      Aprovador     Status     Data Aprova็ใo" 
cabec2    := ""
cabec3    := "" 
cCancel   := "***** CANCELADO PELO OPERADOR *****"
_nPag     := 1  //Variavel que acumula numero da pagina 
M_PAG     := 1
wnrel     := "NHEST201"  
_cPerg	  := "EST201"  


//--------------------------------------------------------------
// PERGUNTAS
//-------------------------------------------------------------- 
// mv_par01 "DE SC"
// mv_par02 "ATE SC"
// mv_par03 "DE PRODUTO"
// mv_par04 "ATE PRODUTO"
// mv_par05 "DE GRUPO"
// mv_par06 "ATE GRUPO" 
// mv_par07 "MOSTRAR SOMENTE SC:"  1 = PENDENTE  2 = APROVADA 3 = AMBOS
//--------------------------------------------------------------

Pergunte("EST201",.T.) // <-- Chama a fun็ใo responsแvel pela inicializa็ใo das "perguntas" do relat๓rio, valor em "TRUE" for็a o usuแrio a entrar com os parโmetros primeiro. 

SetPrint(cString,wnrel,_cPerg,titulo,cDesc1,cDesc2,cDesc3,.F.,,,tamanho)

if nlastKey ==27
    Set Filter to
    Return
Endif

SetDefault(aReturn,cString)

nTipo := IIF(aReturn[4]==1,GetMV("MV_COMP"), GetMV("MV_NORM"))

aDriver := ReadDriver()
cCompac := aDriver[1]      

//-- PROGRAMACAO

Processa( {|| Gerando()   },"Gerando Dados do Relat๓rio...") 
Processa( {|| RptDetail()   },"Gerando Relat๓rio...") 

set filter to 
//set device to screen
If aReturn[5] == 1
	Set Printer To
	Commit
    ourspool(wnrel) //Chamada do Spool de Impressao
Endif                                          
MS_FLUSH() //Libera fila de relatorios em spool

Return

Static Function Gerando() // FUNวรO RESPONSAVEL POR GERAR A QUERY
Local cQuery 

ProcRegua(2)

IncProc()    
    

    //------------------
    // MONTA A QUERY
    //------------------               
    
    cQuery := " SELECT C1_NUM, C1_ITEM, C1_PRODUTO, C1_DESCRI, C1_QUANT, C1_CC, B1_GRUPO, C1_EMISSAO, "   // RETIRAR O TOP 15 QUANDO FOR COMPILAR
    cQuery += " ZU_LOGIN, ZU_NIVEL , ZU_STATUS, ZU_DATAPR " 
    cQuery += " FROM " + RetSqlName("SC1") + " C1, " + RetSqlName("SB1") + " B1, " + RetSqlName("SZU") + " ZU "    
    cQuery += " WHERE C1_PRODUTO = B1_COD "
    cQuery += " AND C1_NUM = ZU_NUM "
    cQuery += " AND C1_ITEM = ZU_ITEM "    
    cQuery += " AND C1.C1_NUM BETWEEN '"+mv_par01+"' AND '"+mv_par02+"' " // DE ACORDO COM AS PERGUNTAS, FILTRA AS SC
    cQuery += " AND C1.C1_PRODUTO BETWEEN '"+mv_par03+"' AND '"+mv_par04+"' " // DE ACORDO COM AS PERGUNTAS, FILTRA OS PRODUTOS
    cQuery += " AND B1.B1_GRUPO BETWEEN '"+mv_par05+"' AND '"+mv_par06+"' " // DE ACORDO COM AS PERGUNTAS, FILTRA OS GRUPOS    
    cQuery += " AND C1.D_E_L_E_T_ = '' AND C1_FILIAL = '" +xFilial("SC1") + "' "
    cQuery += " AND ZU.D_E_L_E_T_ = '' AND ZU_FILIAL = '" +xFilial("SZU") + "' "
    cQuery += " AND ZU_ORIGEM = 'SC1' "
    cQuery += " AND C1_RESIDUO <> 'S' "
    cQuery += " AND ZU_STATUS <> 'C' " // Nao traz SC's Canceladas        
          
   
   // Mostrar Somente SC: ---------------------------------------
   DO CASE    
  
     CASE mv_par07 == 1
       cQuery += " AND ZU_STATUS = '' "
	 CASE mv_par07 == 2
 	   cQuery += " AND ZU_STATUS = 'A' " 
 	   	   
   ENDCASE
  //-------------------------------------------------------------
  
   cQuery += " ORDER BY C1_NUM, C1_ITEM, ZU_NIVEL "
        
   	//MemoWrit('C:\TEMP\NHEST201.SQL',cQuery)
	TCQUERY cQuery NEW ALIAS "TRA1"  // Criando o ALIAS
	
	TRA1->(DbGoTop())    
	
	TcSetField("TRA1","ZU_DATAPR","D")  // Muda a data de string para date
	TcSetField("TRA1","C1_EMISSAO","D")  // Muda a data de string para date 
	
	IncProc()

Return

// MONTA O RELATำRIO (FอSICO) ----------------------------------

Static Function RptDetail()  

Local _dData //Variavel que recebe a data que vai junto do Tํtulo
Local _nSC 
Local _nItem 

ProcRegua(0)

_dData := Date()
                                         
Titulo += DtoC(_dData) // Coloca a data atual no tํtulo do relat๓rio 

Cabec(Titulo, Cabec1, Cabec2, NomeProg, Tamanho, nTipo) //Monta o Cabe็alho do Relat๓rio 

//Escrevendo os dados no relat๓rio -------------------------------------
While TRA1->(!Eof())
	

	If Prow() > 75  // Limite de linhas por pแgina
		Cabec(Titulo, Cabec1, Cabec2, NomeProg, Tamanho, nTipo) // <--  Se for maior que o limite, escreve um cabe็alho
	Endif  
	
    // Verifica se o item da SC contem mais de um aprovador -----
	
	If (_nSC == TRA1->C1_NUM .And. _nItem == TRA1->C1_ITEM)  
		@Prow()+1, 127 psay TRA1->ZU_LOGIN 
		@Prow()  , 145 psay TRA1->ZU_STATUS
		@Prow()  , 153 psay TRA1->ZU_DATAPR
	Else  
		@Prow()+1, 001 psay ''
		@Prow()+1, 001 psay TRA1->C1_NUM  // SC
		@Prow()  , 012 psay TRA1->C1_ITEM  
		@Prow()  , 022 psay TRA1->C1_PRODUTO
		@Prow()  , 042 psay TRA1->C1_DESCRI
		@Prow()  , 076 psay TRA1->C1_QUANT picture("@e 9,999,999.99")
		@Prow()  , 093 psay TRA1->C1_CC
		@Prow()  , 105 psay TRA1->B1_GRUPO
		@Prow()  , 114 psay TRA1->C1_EMISSAO
		@Prow()  , 128 psay TRA1->ZU_LOGIN	
		@Prow()  , 145 psay TRA1->ZU_STATUS
		@Prow()  , 153 psay TRA1->ZU_DATAPR		
	EndIf
	//----------------------------------------------------------  	
		
	_nSC := TRA1->C1_NUM
	_nItem := TRA1->C1_ITEM	
	
	TRA1->(DbSkip()) 	
	   	
EndDo
//------------------------------------------------------------------------

@Prow()+1, 000 PSAY __PrtThinLine() //  <--  Escreve uma linha fina na tela

					
TRA1->(DbCloseArea()) // Encerrando a ALIAS "TRA1" 
  

Return(nil)